buildscript {
    repositories {
        jcenter()
        maven { url 'http://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.4-3'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
apply plugin: 'idea'

sourceCompatibility = 1.8
version = modVersion
group = 'wiresegal.classy.hats'
archivesBaseName = 'ClassyHats'

compileKotlin {
    kotlinOptions {
        javaParameters = true
        jvmTarget = 1.8
    }
}

minecraft {
    version = mcfVersion
    mappings = mcpMappings
    runDir = 'run'

    replaceIn 'LibMisc.java'
    replaceIn 'LibMisc.kt'
    replace '%VERSION%', modVersion

    clientJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
    serverJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
}

repositories {
    maven { url 'http://maven.bluexin.be/repository/snapshots' }
    maven { url 'http://dvs1.progwml6.com/files/maven' }
}

dependencies {
    deobfCompile "com.teamwizardry.librarianlib:librarianlib-$liblibVersion"
    deobfCompile "mezz.jei:jei_$jeiVersion"
}

sourceSets { main.java.srcDirs += 'src/main/kotlin' }

idea {
    module { inheritOutputDirs = true }
    project { languageLevel = '1.8' }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info', 'pack.mcmeta'
        expand 'version': modVersion
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', 'pack.mcmeta'
    }
}
